;Modifies Drawer Price For Conestoga Pricing
;Set DESIGNPR for each style of price
;Check Lineal Inch Prices
;Check Design Prices
;Check Finishing Prices
;By Kaleb Marske 8/3/06
;Modified 4/20/07 to Include Finishing Price and Min. Lineal Inch
;ksmarske@gmail.com    
;2/26/09 Modified for Ginrich Woodcraft drawers by Bruce Kruger

For Each DWR_OPEN opening                 

public debug<int> = 0		;enable debug

b_sizeCutoff<dec> = 6

if this.BF._M:SCHEDID == 60 then 	; White Birch Boxes. Pricing updated 20150522    
	b_GingrichDiscount<dec> = 0.10
	b_CutoffAndUnderCost<dec> = 10.75		;on boxes 6" high and under, cost per square foot - can be discounted depending on order QTY
	b_OverCutoffCost<dec> = 8.96			;on boxes over 6" high, cost per square foot - can be discounted depending on order QTY
	b_BottomCost<dec> = 2.05				;cost per square foot of bottom - can be discounted depending on order QTY

	b_dividerSqFtCost<dec> = 8				; cost per square foot of divider. Never discounted.
	
else
	if this.BF._M:SCHEDID == 489 then 	; Economy Maple Boxes. Pricing updated 20150522    
		b_GingrichDiscount<dec> = 0.10
		b_CutoffAndUnderCost<dec> = 7.50		;on boxes 6" high and under, cost per square foot - can be discounted depending on order QTY
		b_OverCutoffCost<dec> = 6.00			;on boxes over 6" high, cost per square foot - can be discounted depending on order QTY
		b_BottomCost<dec> = 1.95				;cost per square foot of bottom - can be discounted depending on order QTY
	
		b_dividerSqFtCost<dec> = 8				; cost per square foot of divider. Never discounted.
		
	end if
end if	

if this.BF.CONSTID == 23 OR this.BF.CONSTID == 25 then		; Gingrich drawers with Blum notching
		b_MachiningCost<dec> = 10				;cost per box for notching and finishing. Never discounted.
		b_singleDivSetup<dec> = 15      		; setup cost for single divider. Never discounted.
		b_multipleDivSetup<dec> = 25			; setup cost for multiple dividers. Never discounted.
		b_SetupCost<dec> = 0					; miscellaneous setup cost. Never discounted.
else
	if this.BF.CONSTID == 29 then 				; Gingrich drawers with NO notching	
		b_MachiningCost<dec> = 8				;cost per box for notching and finishing. Never discounted.	
		b_singleDivSetup<dec> = 15      		; setup cost for single divider. Never discounted.
		b_multipleDivSetup<dec> = 25			; setup cost for multiple dividers. Never discounted.
		b_SetupCost<dec> = 0					; miscellaneous setup cost. Never discounted.
	else
		if this.BF.CONSTID == 36 then 				; Gingrich corner drawers with  notching	
			b_MachiningCost<dec> = 8				;cost per box for notching and finishing. Never discounted.	
			b_singleDivSetup<dec> = 15      		; setup cost for single divider. Never discounted.
			b_multipleDivSetup<dec> = 25			; setup cost for multiple dividers. Never discounted.
			
			; corner drawer setup cost
			b_SetupCost<dec> = 70					; miscellaneous setup cost. Never discounted.
		end if	
	end if
end if		

	

if this.b_AccessoryCost == NULL then
	b_AccessoryCost<dec> = 0
end if	
    
    
    
;If this.BF.constID == 23 OR this.BF.constID == 25 Then

	b_AreaSides<dec> := 2*(boxw * boxh)/144 + 2*(boxd * boxh)/144
	b_AreaBottom<dec> := boxw * boxd / 144              
	
	If boxH <= b_sizeCutoff then ;b_sizeCutoff then
		b_BoxSidesCost<dec> := b_AreaSides * b_CutoffAndUnderCost
	else
		b_BoxSidesCost<dec> := b_AreaSides * b_OverCutoffCost
	end if    
	
	b_BoxBottomCost<dec> := b_AreaBottom * b_BottomCost 
	b_materialCost<dec> := (b_BoxBottomCost + b_BoxSidesCost) * (1 - b_GingrichDiscount)       
	   
	if this.DWR._dwr_AccessoryCost != NULL then
		b_AccessoryCost<dec> := this.DWR._dwr_AccessoryCost  
	end if  
	
	
	
	
	;search for partitions and dividers here
	b_DividerCount<int> = 0
	b_dividerTotalCost<dec> = 0
	temp_loop<int> = 1
	while this.S_BDI@{temp_loop}.PID > 0 do
		;b_boxDividers += b_dividerCost
		b_dividerTotalCost += (this.S_BDI@{temp_loop}.DX * this.S_BDI@{temp_loop}.DY / 144) * b_dividerSqFtCost
		b_DividerCount += 1
		temp_loop += 1
	end while
	
	temp_loop<int> = 1
	while this.S_BPT@{temp_loop}.PID > 0 do
		b_dividerTotalCost += (this.S_BPT@{temp_loop}.DX * this.S_BPT@{temp_loop}.DY / 144) * b_dividerSqFtCost
		b_DividerCount += 1
		temp_loop += 1
	end while    
	
	delete temp_loop  
	           
	           
	if b_dividerCount > 0 then
		if b_dividerCount >= 2 then 
			b_dividerTotalCost<dec> += b_multipleDivSetup
		else
			b_dividerTotalCost<dec> += b_singleDivSetup
		end if			
	end if			 		
		
	PRICE<dec> := b_MachiningCost + b_SetupCost + b_materialCost + b_AccessoryCost + b_dividerTotalCost
	;only discount materials. machine cost is never discounted.

if debug == TRUE then                              
	dim DRAWER as new part                  
		DRAWER.owner = cab
		DRAWER.name = 'Drawer Price = ${PRICE}' 
		DRAWER.visible = FALSE
		
	dim DRAWER1 as new part                  
		DRAWER1.owner = cab
		DRAWER1.name = 'Divider cost = ${b_dividerTotalCost}' 
		DRAWER1.visible = FALSE
end if
